singleQuery = { SOI ~ queryPrimary ~ EOI}
singleExpression = { SOI ~ expression ~ EOI}
singleDataType = { SOI ~ dataType ~ EOI}
singleTableSchema = { SOI ~ colTypeList ~ EOI}

queryPrimary = {
    selectClause
    ~ fromClause?
    ~ lateralView?
    ~ whereClause?
    ~ aggregationClause?
}

selectClause = { ^"select" ~ namedExpressionSeq}
fromClause = { ^"from" ~ identifier }
whereClause = { ^"where" ~ booleanExpression }
lateralView = { ^"lateral"~ ^"view" ~ OUTER? ~ identifier ~ functionArgs ~ identifier ~ (^"as"? ~ lateralViewcolName ~ ("," ~ lateralViewcolName)* )? }
lateralViewcolName = { !mainKeyword ~ identifier }
aggregationClause = { ^"group"~ ^"by" ~ expression ~ ("," ~ expression)* }

relationPrimary = { identifier ~ (^"as"? ~ !mainKeyword ~ identifier)? }

namedExpressionSeq = { namedExpression ~ ("," ~ namedExpression)*}
namedExpression = {
    expression ~ (^"as")?  ~ !mainKeyword  ~ identifier
    | expression
}
expression = { booleanExpression }

// 逻辑表达式（逻辑层）
booleanExpression = { logicalOrExpression }

logicalOrExpression = { logicalAndExpression ~ (^"or" ~ logicalAndExpression)* }
logicalAndExpression = { logicalNotExpression ~ (^"and" ~ logicalNotExpression)* }
logicalNotExpression = { NOT? ~ predicateExpression }
predicateExpression = { valueExpression ~ predicate? }

predicate = {
    predicateBetween
    | predicateRlike
    | predicateLike
    | predicateIn
    | predicateNull
}

predicateBetween = { NOT? ~ ^"between" ~ valueExpression ~ ^"and" ~ valueExpression }
predicateRlike = { NOT? ~ ^"rlike" ~ valueExpression}
predicateLike = { NOT? ~ ^"like" ~ valueExpression}
predicateIn = { NOT? ~ ^"in" ~ "(" ~ expression ~ ("," ~ expression)* ~ ")"}
predicateNull = {^"is" ~ NOT? ~ ^"null"}

// 值表达式（算术层）
valueExpression = { comparisonExpression }

comparisonExpression = { bitOrExpression ~ (comparisonOperator ~ bitOrExpression)? }
// 位或（|）
bitOrExpression = { bitXorExpression ~ (PIPE ~ bitXorExpression)* }
// 位异或（^）
bitXorExpression = { bitAndExpression ~ (HAT ~ bitAndExpression)* }
// 位与（&）
bitAndExpression = { bitShiftExpression ~ (AMPERSAND ~ bitShiftExpression)* }
// 位移（<<, >>, >>>）
bitShiftExpression = { addSubExpression ~ ((SHIFT_LEFT | SHIFT_RIGHT | SHIFT_RIGHT_UNSIGNED) ~ addSubExpression)* }
// 加减法
addSubExpression = { mulDivExpression ~ ((PLUS | MINUS) ~ mulDivExpression)* }
// 乘除法
mulDivExpression = { unaryExpression ~ ((ASTERISK | SLASH | PERCENT) ~ unaryExpression)* }
// 一元表达式
unaryExpression = { (MINUS | PLUS | TILDE)? ~ primaryExpression }

// 基本表达式
primaryExpression = { atomicExpression ~ (subscriptOp | dereferenceOp)* }
atomicExpression  = {
    searchedCase
    | simpleCase
    | cast
    | functionCall
    | constant
    | columnReference
    | "(" ~ expression ~ ")"
}
columnReference = { identifier }
subscriptOp = { "[" ~ valueExpression ~ "]" }
dereferenceOp = { "." ~ identifier }

functionCall = {
    identifier ~ functionArgs
}

functionArgs = {
    "(" ~ (expression ~ ("," ~ expression)*)? ~ ")"
}

searchedCase = { ^"case" ~ whenClause +  ~ ("else" ~ expression)? ~ ^"end" }
simpleCase = { ^"case" ~ expression ~ whenClause +  ~ ("else" ~ expression)? ~ ^"end" }
whenClause = { ^"when" ~ expression ~ ^"then" ~ expression}

cast = { ^"cast" ~ "(" ~ expression ~ ^"as" ~ dataType  ~ ")" }

dataType = {
    arrayDataType
    | structDataType
    | primitiveDataType
}
arrayDataType = { ^"array" ~ "<"  ~ dataType ~ ">" }
structDataType = { ^"struct" ~ "<"  ~ complexColType ~ ("," ~ complexColType)* ~ ">" }
primitiveDataType = { identifier }

colTypeList = { colType ~ ("," ~ colType)* }
colType = { identifier ~ dataType ~ (^"not" ~ ^"null")? }
complexColType = { identifier ~ ":" ~ dataType }

constant = { NULL | number | booleanValue | STRING }

qualifiedName = { identifier ~ ("." ~ identifier)* }
identifier = {unquotedIdentifier | quotedIdentifier}
unquotedIdentifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | ASCII_DIGIT | "_")* } // [a-zA-Z0-9_]+
quotedIdentifier = { "`" ~ (!"`" ~ ANY)* ~ "`"}

number = {
    doubleLiteral
    | floatLiteral
    | decimalLiteral
    | bigIntLiteral
    | integerLiteral
}

integerLiteral = @{ MINUS? ~ INTEGER_VALUE }
bigIntLiteral = @{ MINUS? ~ BIGINT_LITERAL }
decimalLiteral = @{ MINUS? ~ DOUBLE_VALUE }
floatLiteral = @{ MINUS? ~ FLOAT_LITERAL }
doubleLiteral = @{ MINUS? ~ DOUBLE_LITERAL }

INTEGER_VALUE = @{ ASCII_DIGIT+ }
BIGINT_LITERAL = @{ ASCII_DIGIT+ ~ ^"L" }
DOUBLE_VALUE = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
FLOAT_LITERAL = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ^"F" }
DOUBLE_LITERAL = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ^"D" }


booleanValue = { ^"true" | ^"false" }
STRING = ${
    "'" ~ (!"'" ~ ANY)*  ~ "'"
    | "\"" ~ (!"\"" ~ ANY)*  ~ "\""
}

NOT = { ^"not" | "!"}
OUTER = { ^"outer"}

// arithmeticOperator = { PLUS | MINUS | ASTERISK | SLASH | PERCENT }
NULL = { ^"null" }
PLUS = { "+" }
MINUS = { "-" }
ASTERISK = { "*" }
SLASH = { "/" }
PERCENT = { "%" }
TILDE = { "~" }
AMPERSAND = { "&" }
PIPE = { "|" }
HAT = { "^" }

SHIFT_LEFT = { "<<" }
SHIFT_RIGHT = { ">>" }
SHIFT_RIGHT_UNSIGNED = { ">>>" }

comparisonOperator = { EQ | NEQ | NEQJ | NSEQ | LT | LTE | GT | GTE  }
EQ = { "=" | "=="}
NEQ = { "<>"}
NEQJ = { "!="}
NSEQ = { "<=>"}
LT = { "<" }
LTE = { "<=" | "!>"}
GT = { ">" }
GTE = { ">=" | "!<"}

mainKeyword = { ^"from" | ^"where" | ^"order" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ SIMPLE_COMMENT | BRACKETED_COMMENT }

SIMPLE_COMMENT = _{ "--" ~ ("\\\n" | (!"\n" ~ ANY))* ~ ("\r"? ~ "\n")? }
BRACKETED_COMMENT =  _{ "/*" ~ (!"*/" ~ (BRACKETED_COMMENT | ANY))* ~ "*/" }